plugins {
    id 'java'
    id 'me.champeau.jmh' version '0.7.3'
}

repositories {
    mavenCentral()
    maven {
        name = "StaticStudios"
        url = 'https://repo.staticstudios.net/snapshots/'
    }
}

dependencies {
    implementation("org.openjdk.jmh:jmh-core:1.37")
    implementation("org.openjdk.jmh:jmh-generator-annprocess:1.37")
    implementation(project(":core"))
    implementation 'net.staticstudios:static-utils:1.0.6-SNAPSHOT'
    implementation("org.testcontainers:postgresql:1.19.8")
    implementation("com.redis:testcontainers-redis:2.2.2")
    implementation("org.slf4j:slf4j-log4j12:2.0.16")

    annotationProcessor project(":processor")
    compileOnly project(":processor")
}

def generatedSourcesDir = file("$buildDir/generated/sources/annotations")
sourceSets.main.java.srcDir(generatedSourcesDir)


tasks.register('runAnnotationProcessor', JavaCompile) {
    group = 'build'
    description = 'Run annotation processor manually (codegen only)'

    source = sourceSets.main.java
    classpath = configurations.compileClasspath + configurations.annotationProcessor
    destinationDir = file("$buildDir/tmp/classes/manual") // arbitrary, ignored with -proc:only

    options.annotationProcessorPath = configurations.annotationProcessor
    options.compilerArgs += [
            '-proc:only',
            '-processor', 'net.staticstudios.data.processor.DataProcessor',
            '-s', generatedSourcesDir
    ]
}

tasks.named('jmhRunBytecodeGenerator') {
    outputs.upToDateWhen { false }
}

tasks.named('jmh') {
    jvmArgs = [
            '-Xms1g',
            '-Xmx1g',
            '-XX:+AlwaysPreTouch',
            '-Djmh.ignoreLock=true'
    ]
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
