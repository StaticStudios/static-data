plugins {
    id 'java'
}

allprojects {
    group = 'net.staticstudios'
    version = '3.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        maven {
            name = "StaticStudios"
            url = "https://repo.staticstudios.net/snapshots/"
        }
    }

    java {
        targetCompatibility = JavaVersion.VERSION_21
        sourceCompatibility = JavaVersion.VERSION_21
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }
}

subprojects {
    plugins.withId('maven-publish') {
        publishing {
            repositories {
                maven {
                    credentials(org.gradle.api.credentials.PasswordCredentials.class)
                    name = "StaticStudios"
                    var base = "https://repo.staticstudios.net"
                    var releasesRepoUrl = "$base/releases/"
                    var snapshotsRepoUrl = "$base/snapshots/"
                    setUrl((version.toString().endsWith("SNAPSHOT")) ? snapshotsRepoUrl : releasesRepoUrl)
                }
            }
        }

        rootProject.tasks.named("publish").configure {
            dependsOn(tasks.named("publish"))
        }

        rootProject.tasks.named("publishToMavenLocal").configure {
            dependsOn(tasks.named("publishToMavenLocal"))
        }
    }
}

tasks.register("publish") {
    group = "publishing"
    description = "Publishes all modules to the StaticStudios Maven repository."
}

tasks.register("publishToMavenLocal") {
    group = "publishing"
    description = "Publishes all modules to Maven Local."
}
